///! Platform host that implements effectful functions for stdout, stderr, and stdin.
const std = @import("std");
const builtin = @import("builtin");
const abi = @import("roc_platform_abi.zig");
const tb_host = @import("tb_host.zig");

pub const std_options: std.Options = .{
    .allow_stack_tracing = false,
};

/// Host environment. Embeds `abi.RocEnv` so the Roc runtime sees a pointer
/// to a standard `RocEnv` while hosted functions can recover the full
/// `HostEnv` via `@fieldParentPtr`.
const HostEnv = struct {
    gpa: std.heap.DebugAllocator(.{}),
    stdin_reader: std.Io.File.Reader,
    roc_env: abi.RocEnv,
};

/// Roc entrypoint exported by the app under `provides { "roc_main": main_for_host! }`.
extern fn roc_main(args: abi.RocList(abi.RocStr)) callconv(.c) i32;

/// Private RocHost used by host helpers and exported runtime symbols.
var g_roc_host: ?*abi.RocHost = null;

// OS-specific entry point handling (not exported during tests)
comptime {
    if (!builtin.is_test) {
        // Export main for all platforms
        @export(&main, .{ .name = "main" });

        // Windows MinGW/MSVCRT compatibility: export __main stub
        if (@import("builtin").os.tag == .windows) {
            @export(&__main, .{ .name = "__main" });
        }
    }
}

// Windows MinGW/MSVCRT compatibility stub
// The C runtime on Windows calls __main from main for constructor initialization
fn __main() callconv(.c) void {}

// C compatible main for runtime
fn main(argc: c_int, argv: [*][*:0]u8) callconv(.c) c_int {
    return platform_main(@intCast(argc), argv);
}

/// Hosted function: Stderr.line!
fn hostedStderrLine(str: abi.RocStr) callconv(.c) void {
    const host = g_roc_host.?;
    var owned = str;
    defer owned.decref(host);

    const message = owned.asSlice();
    const io = std.Io.Threaded.global_single_threaded.io();
    const stderr = std.Io.File.stderr();
    stderr.writeStreamingAll(io, message) catch {};
    stderr.writeStreamingAll(io, "\n") catch {};
}

/// Hosted function: Stdin.line!
fn hostedStdinLine() callconv(.c) abi.RocStr {
    const host = g_roc_host.?;
    const roc_env: *abi.RocEnv = @ptrCast(@alignCast(host.env));
    const host_env: *HostEnv = @fieldParentPtr("roc_env", roc_env);
    var reader = &host_env.stdin_reader.interface;

    var line = while (true) {
        const maybe_line = reader.takeDelimiter('\n') catch |err| switch (err) {
            error.ReadFailed => break &.{}, // Return empty string on error
            error.StreamTooLong => {
                // Skip the overlong line so the next call starts fresh.
                _ = reader.discardDelimiterInclusive('\n') catch |discard_err| switch (discard_err) {
                    error.ReadFailed, error.EndOfStream => break &.{},
                };
                continue;
            },
        } orelse break &.{};

        break maybe_line;
    };

    // Trim trailing \r for Windows line endings
    if (line.len > 0 and line[line.len - 1] == '\r') {
        line = line[0 .. line.len - 1];
    }

    if (line.len == 0) {
        return abi.RocStr.empty();
    }

    return abi.RocStr.fromSlice(line[0..line.len], host);
}

/// Hosted function: Stdout.line!
fn hostedStdoutLine(str: abi.RocStr) callconv(.c) void {
    const host = g_roc_host.?;
    var owned = str;
    defer owned.decref(host);

    const message = owned.asSlice();
    const io = std.Io.Threaded.global_single_threaded.io();
    const stdout = std.Io.File.stdout();
    stdout.writeStreamingAll(io, message) catch {};
    stdout.writeStreamingAll(io, "\n") catch {};
}

/// Hosted function: Utc.now!
fn hostedHostPosixTime() callconv(.c) i128 {
    const io = std.Io.Threaded.global_single_threaded.io();

    const nanos = std.Io.Clock.real.now(io).toNanoseconds();
    return @intCast(nanos);
}

fn hostedHostRandomU64() callconv(.c) u64 {
    const io = std.Io.Threaded.global_single_threaded.io();
    var buf = [_]u8{0} ** 8;
    std.Io.random(io, &buf);

    return @bitCast(buf);
}

fn hostedHostRandomU128() callconv(.c) u128 {
    const io = std.Io.Threaded.global_single_threaded.io();
    var buf = [_]u8{0} ** 16;
    std.Io.random(io, &buf);

    return @bitCast(buf);
}

fn hostAlloc(length: usize, alignment: usize) callconv(.c) ?*anyopaque {
    return abi.DefaultAllocators.rocAlloc(g_roc_host.?, length, alignment);
}

fn hostDealloc(ptr: *anyopaque, alignment: usize) callconv(.c) void {
    abi.DefaultAllocators.rocDealloc(g_roc_host.?, ptr, alignment);
}

fn hostRealloc(ptr: *anyopaque, new_length: usize, alignment: usize) callconv(.c) ?*anyopaque {
    return abi.DefaultAllocators.rocRealloc(g_roc_host.?, ptr, new_length, alignment);
}

fn hostDbg(bytes: [*]const u8, len: usize) callconv(.c) void {
    abi.DefaultHandlers.rocDbg(g_roc_host.?, bytes, len);
}

fn hostExpectFailed(bytes: [*]const u8, len: usize) callconv(.c) void {
    abi.DefaultHandlers.rocExpectFailed(g_roc_host.?, bytes, len);
}

fn hostCrashed(bytes: [*]const u8, len: usize) callconv(.c) void {
    abi.DefaultHandlers.rocCrashed(g_roc_host.?, bytes, len);
}

comptime {
    if (!builtin.is_test) {
        @export(&hostedStderrLine, .{ .name = "roc_stderr_line", .visibility = .hidden });
        @export(&hostedStdinLine, .{ .name = "roc_stdin_line", .visibility = .hidden });
        @export(&hostedStdoutLine, .{ .name = "roc_stdout_line", .visibility = .hidden });
        @export(&hostedHostPosixTime, .{ .name = "roc_host_posix_time", .visibility = .hidden });
        @export(&hostedHostRandomU64, .{ .name = "roc_host_random_U64", .visibility = .hidden });
        @export(&hostedHostRandomU128, .{ .name = "roc_host_random_U128", .visibility = .hidden });

        @export(&hostAlloc, .{ .name = "roc_alloc", .visibility = .hidden });
        @export(&hostDealloc, .{ .name = "roc_dealloc", .visibility = .hidden });
        @export(&hostRealloc, .{ .name = "roc_realloc", .visibility = .hidden });
        @export(&hostDbg, .{ .name = "roc_dbg", .visibility = .hidden });
        @export(&hostExpectFailed, .{ .name = "roc_expect_failed", .visibility = .hidden });
        @export(&hostCrashed, .{ .name = "roc_crashed", .visibility = .hidden });
    }
}

// TigerBeetle hosted functions live in tb_host.zig; importing it above pulls in
// its `roc_tb_create_accounts` export. The init/deinit calls below are the only
// coupling — the heavy TB code stays out of this file.

/// Platform host entrypoint
fn platform_main(argc: usize, argv: [*][*:0]u8) c_int {
    const io = std.Io.Threaded.global_single_threaded.io();
    var stdin_buffer: [4096]u8 = undefined;

    var host_env = HostEnv{
        .gpa = std.heap.DebugAllocator(.{}){},
        .stdin_reader = std.Io.File.stdin().readerStreaming(io, &stdin_buffer),
        .roc_env = undefined,
    };
    host_env.roc_env = .{
        .allocator = host_env.gpa.allocator(),
        .roc_io = abi.RocIo.default(),
    };

    var roc_host = abi.makeRocHost(&host_env.roc_env);
    g_roc_host = &roc_host;
    tb_host.init(&roc_host, host_env.gpa.allocator());

    // Build List(Str) from argc/argv
    std.log.debug("[HOST] Building args...", .{});
    const args_list = buildStrArgsList(argc, argv, &roc_host);
    std.log.debug("[HOST] args_list ptr=0x{x} len={d}", .{ @intFromPtr(args_list.elements_ptr), args_list.length });

    // Call the app's main! entrypoint - returns I32 exit code
    std.log.debug("[HOST] Calling roc_main...", .{});

    const exit_code = roc_main(args_list);
    std.log.debug("[HOST] Returned from roc, exit_code={d}", .{exit_code});

    // Tear down the shared TigerBeetle client, if one was created.
    tb_host.deinitClient();

    // Check for memory leaks before returning
    const leak_status = host_env.gpa.deinit();
    if (leak_status == .leak) {
        std.log.err("\x1b[33mMemory leak detected!\x1b[0m", .{});
        std.process.exit(1);
    }

    return exit_code;
}

/// Build a RocList of RocStr from argc/argv
fn buildStrArgsList(argc: usize, argv: [*][*:0]u8, roc_host: *abi.RocHost) abi.RocList(abi.RocStr) {
    if (argc == 0) {
        return abi.RocList(abi.RocStr).empty();
    }

    const args_list = abi.RocList(abi.RocStr).allocate(argc, roc_host);
    const args_ptr: [*]abi.RocStr = args_list.elements_ptr.?;

    // Build each argument string
    for (0..argc) |i| {
        const arg_cstr = argv[i];
        const arg_len = std.mem.len(arg_cstr);
        args_ptr[i] = abi.RocStr.fromSlice(arg_cstr[0..arg_len], roc_host);
    }

    return args_list;
}
